//******************************************************************************
Процедура ПробитьЧекНаККМ()
	
	Если ЧекПробитККМ = 1 Тогда
		Сообщить("По этому документу уже пробит чек!");
		Возврат;
	КонецЕсли;
	
	СписокТоваров = СоздатьОбъект("ТаблицаЗначений");
	СписокТоваров.НоваяКолонка("ТовНаим"   , "Строка");
	СписокТоваров.НоваяКолонка("Цена"      , "Число");
	СписокТоваров.НоваяКолонка("Количество", "Число");
	СписокТоваров.НоваяКолонка("Сумма"	   , "Число");
	СписокТоваров.НоваяКолонка("СтавкаНДС" , "Строка");
	
	СписокТоваров.НоваяКолонка("ПризнакСпособаРасчета" , "Число");
	СписокТоваров.НоваяКолонка("ПризнакПредметаРасчета", "Число");
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		СписокТоваров.НоваяСтрока();
		СписокТоваров.ТовНаим    = Товар.Наименование;
		СписокТоваров.Цена       = Цена;
		СписокТоваров.Количество = Количество; 
		СписокТоваров.Сумма		 = Сумма;
		СписокТоваров.СтавкаНДС  = "none";
		
		СписокТоваров.ПризнакСпособаРасчета  = 4;
		СписокТоваров.ПризнакПредметаРасчета = 1;
		
	КонецЦикла;

	ПризнВозврата = 0;
	КассирФИО = ПолноеИмяПользователя();
	СуммаЧека = Итог("Сумма");
	
	Параметры = СоздатьОбъект("СписокЗначений");
	Параметры.Установить("Процесс"           , "печатать_чек");
	Параметры.Установить("СписокТоваров"     , СписокТоваров);
	Параметры.Установить("Возврат"           , ПризнВозврата);
	Параметры.Установить("КассирФИО" 		 , КассирФИО);
	Параметры.Установить("КонтактКлиента"    , КонтактКлиента);
	
	Если ОплатаКартой = 0 Тогда
		Параметры.Установить("СуммаНаличными", СуммаЧека);
	ИначеЕсли ОплатаКартой = 1 Тогда
		Параметры.Установить("СуммаЭлектронными", СуммаЧека);
	КонецЕсли;
		
	ОткрытьФормуМодально("Отчет", Параметры, КаталогИБ() + "ONLINE-54.ert");
	
	Если Параметры.Получить("Результат") = 1 Тогда
		ЧекПробитККМ=1;
		ПриЗаписиПерепроводить(0);
		Записать();
		ПриЗаписиПерепроводить(1);
	Иначе
		ЧекПробитККМ=0;
		Сообщить(Параметры.Получить("ОписаниеРезультата"));
	КонецЕсли;
	
КонецПроцедуры // ПробитьЧекНаККМ()